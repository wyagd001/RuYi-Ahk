;|2.9|2024.12.23|1350
#Include <WebView2>

Persistent
CandySel := CandySel2 := ""
Try CandySel := A_Args[1],  CandySel2 := A_Args[2]
if !CandySel
{
	DetectHiddenWindows 1
	Try CandySel := ControlGetText("Edit1", "获取当前窗口信息_") 
	DetectHiddenWindows 0
}
OnMessage 0x4a, Receive_WM_COPYDATA

Text1 := definefunc(CandySel)
;msgbox text1
return

comparetext(fp:="")
{
	Global
	main := Gui("+Resize")
	main.OnEvent("Close", _ExitApp)
  if FileExist(CandySel)
    SplitPath CandySel, &OutFileName
  if FileExist(fp)
    SplitPath fp, &OutFileName2
	main.Title := OutFileName " - " OutFileName2 " 文本比较"
	main.Show(Format("w{} h{}", A_ScreenWidth * 0.8, A_ScreenHeight * 0.6))

	wvc := WebView2.CreateControllerAsync(main.Hwnd).await2()
	;msgbox main.Hwnd
	wv := wvc.CoreWebView2
	nwr := wv.NewWindowRequested(NewWindowRequestedHandler)
	wv.Navigate(A_ScriptDir "\..\..\临时目录\文本对比.html")

	NewWindowRequestedHandler(wv2, arg) {
    deferral := arg.GetDeferral()
    arg.NewWindow := wv2
		deferral.Complete()
	}

	_ExitApp(*) {
		ExitApp(0)
	}
}

Receive_WM_COPYDATA(wParam, lParam, msg, hwnd)
{
	Global CandySel, text2
	StringAddress := NumGet(lParam, 2*A_PtrSize, "Ptr")  ; 获取 CopyDataStruct 的 lpData 成员.
	CandySel2 := StrGet(StringAddress)  ; 从结构中复制字符串.
  if (CandySel2 = CandySel)
    return 0
	text2 := definefunc(CandySel2)
	;msgbox text2
	if text2
	{
		generatehtml()
		comparetext(CandySel2)
	}
  return true
}

generatehtml()
{
  Global text1, text2
  html_code := 
(
'<!DOCTYPE html>
<html lang="zh-cmn-Hans">
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />

<head>
    <meta charset="UTF-8">
    <title>文本比对</title>
    <link rel="stylesheet" href="../引用程序/其它资源/WebView2/comp/codemirror.min.css">
    <link rel="stylesheet" href="../引用程序/其它资源/WebView2/comp/merge.css">
    <script
        src="../引用程序/其它资源/WebView2/comp/jquery.min.js">
    </script>
    <style>
    .CodeMirror-merge, .CodeMirror-merge .CodeMirror {
	    height: 98vh;
	}
    </style>
</head>

<body>
    <div id="code_view"></div>


    <script
        src="../引用程序/其它资源/WebView2/comp/diff_match_patch.js">
    </script>
    <script
        src="../引用程序/其它资源/WebView2/comp/codemirror.min.js">
    </script>
    <script
        src="../引用程序/其它资源/WebView2/comp/merge.js">
    </script>

    <script>
        var leftValue = "' text1 '";
        var rightValue = "'  text2 '";
        var t = document.getElementById("code_view");
        CodeMirror.MergeView(t, {
            value: leftValue,
            lineWrapping: true,
            origLeft: null,
            orig: rightValue,
            lineNumbers: true,
            mode: "text/plain",
            highlightDifferences: true,
            connect: null,
            collapseIdentical: false,
            allowEditingOriginals: true
        })
    </script>
</body>

</html>
)'
try FileDelete A_ScriptDir "\..\..\临时目录\文本对比.html"
sleep 300
FileAppend html_code, A_ScriptDir "\..\..\临时目录\文本对比.html", "`UTF-8"
}

definefunc(Str := "")
{
	if FileExist(Str)
		Text := FileRead(Str)
	else
		Text :=  Str
	Text := strreplace(Text, "\", "\\")
	Text := strreplace(Text, "/", "\/")
	Text := strreplace(Text, '"', '\"')
	Text := strreplace(Text, "'", "\'")
	Text := strreplace(Text, "`r`n", "\n")
	return Text
}